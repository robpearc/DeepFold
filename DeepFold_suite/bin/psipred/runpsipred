#!/bin/tcsh

# This is a simple script which will carry out all of the basic steps
# required to make a PSIPRED prediction. Note that it assumes that the
# following programs are in the appropriate directories:
# blastpgp - PSIBLAST executable (from NCBI toolkit)
# makemat - IMPALA utility (from NCBI toolkit)
# psipred - PSIPRED V4 program
# psipass2 - PSIPRED V4 program

# NOTE: Script modified to be more cluster friendly (DTJ April 2008)
set bindir = `readlink -m $0`
set bindir = `dirname $bindir`

# The name of the BLAST data bank
set dbname = `readlink -m $bindir/../../dat/uniref90/uniref90.fasta`
#set dbname = uniref90

# Where the NCBI programs have been installed
set ncbidir = `readlink -m $bindir/../blast/bin`
#set ncbidir = /usr/local/bin

# Where the PSIPRED V4 programs have been installed
set execdir = $bindir/bin
#set execdir = ./bin

# Where the PSIPRED V4 data files have been installed
set datadir = $bindir/data
#set datadir = ./data

set basename = $1:r
set rootname = $basename:t

# Generate a "unique" temporary filename root
set hostid = `hostid`
set tmproot = psitmp$$$hostid

if (-f $basename.mtx ) then
    \cp $basename.mtx $tmproot.mtx
else
    \cp -f $1 $tmproot.fasta

    echo "Running PSI-BLAST with sequence" $1 "..."

    $ncbidir/blastpgp -b 0 -v 5000 -j 3 -h 0.001 -d $dbname -i $tmproot.fasta -C $tmproot.chk >& $tmproot.blast

    if ($status != 0) then
        tail $tmproot.blast
        echo "FATAL: Error whilst running blastpgp - script terminated!"
        exit $status
    endif

    echo $tmproot.chk > $tmproot.pn
    echo $tmproot.fasta > $tmproot.sn

    $ncbidir/makemat -P $tmproot

    if ($status != 0) then
        echo "FATAL: Error whilst running makemat - script terminated!"
        exit $status
    endif
endif

echo "Predicting secondary structure..."

echo Pass1 ...

$execdir/psipred $tmproot.mtx $datadir/weights.dat $datadir/weights.dat2 $datadir/weights.dat3 > $rootname.ss

if ($status != 0) then
    echo "FATAL: Error whilst running psipred - script terminated!"
    exit $status
endif

echo Pass2 ...

$execdir/psipass2 $datadir/weights_p2.dat 1 1.0 1.0 $rootname.ss2 $rootname.ss > $rootname.horiz

if ($status != 0) then
    echo "FATAL: Error whilst running psipass2 - script terminated!"
    exit $status
endif

# Remove temporary files

echo Cleaning up ...
\cp -f $tmproot.mtx $rootname.mtx
\rm -f $tmproot.* error.log

echo "Final output files:" $rootname.ss2 $rootname.horiz
echo "Finished."
